def readPropertyFile(relativePath) {
    StringReader appReader = new StringReader(file("${projectDir.getAbsolutePath()}${relativePath}").getText("utf-8"))
//    StringReader appReader = new StringReader(file("${relativePath}").getText("utf-8"))
    Properties properties = new Properties()
    properties.load(appReader)
    return properties
}

//声明Android程序
apply plugin: 'com.android.application'

android {
    //编译版本
    compileSdkVersion 23
    //build tools版本
    buildToolsVersion "23.0.2"

    defaultConfig {
        //应用包名
        applicationId "com.example.mysmallexample"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    //gradle lint:gradle中有lint任务，可以直接执行lint静态代码检查，但是前提是你的build.gradle设置了lintOptions选项:
    lintOptions {
        //checkReleaseBuilds就是在打包Release版本的时候进行检测，
        // 这里就直接关掉了，也可以打开，这样报错还会显示出来。
        // 关键的就是abortOnError一定要设为false，这样即使有报错也不会停止打包了。
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        myConfig {
            Properties properties = readPropertyFile("/configs/signature.txt")
//            Properties properties = readPropertyFile("C:/Users/taoshuang/Desktop/sign.txt")
            storeFile file(properties.getProperty("storeFile"))
            storePassword properties.getProperty("storePassword")
            keyAlias properties.getProperty("keyAlisa")
            keyPassword properties.getProperty("keyPassword")
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.myConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.myConfig
            minifyEnabled true
            shrinkResources true
            //此属性配合minifyEnabled混淆使用
            //但是如果项目里有用到这个方法
            //getResources().getIdentifier(key, “drawable”,getPackageName()));就会get不到。
            //minifyEnabled  true 是开启混淆
            //shrinkResources  true删除无效的Resource
            //shrinkResources依赖于minifyEnabled，必须和minifyEnabled一起用。
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    //5.0设计兼容库
    compile 'com.android.support:design:23.3.0'
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'de.greenrobot:eventbus:2.4.0'
    //    compile project(':makejarlib')
    compile files('libs/myUtils.jar')
}
